// Ball.jack

/**
 * Implements a graphic ball. A graphic ball has a screen location
 * and a size. It also has methods for drawing, erasing, and moving on the 
 * screen.
 */
class Ball {

    // Location on screen
    field int x, y, x_sim, y_sim;

    // The size of the ball
    field int size;
    
    // The change in position of the ball
    field int delta_x, delta_y;
    
    // Hitbox of the ball
    field Hitbox hitbox;
    
    // Collision flag
    field boolean collision;
        
    /** Constructs a new ball with a given location, size, and delta location. */
    constructor Ball new(int Ax, int Ay, int Asize, int Adelta_x, int Adelta_y) {
        let x = Ax;
        let y = Ay;
        let size = Asize;
        let delta_x = Adelta_x;
        let delta_y = Adelta_y;
        
        let hitbox = Hitbox.new(x + size, x - size, y + size, y - size);
        let collision = false;

        do draw();

        return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Draws the ball on the screen. */
    method void draw() {
        do Screen.setColor(true);
        do Screen.drawCircle(x, y, size);
        return;
    }

    /** Erases the ball from the screen. */
    method void erase() {
        do Screen.setColor(false);
        do Screen.drawCircle(x, y, size);
        return;
    }
    
    /** Simulates the movement of the ball on the screen. */
    method void moveBallSim() {
        let x_sim = x + delta_x;
        let y_sim = y + delta_y;
        do hitbox.update(x_sim + size, x_sim - size, y_sim + size, y_sim - size);
        return;
    }
    
    /** Returns a hitbox of the ball */
    method Hitbox getHitbox() {
        return hitbox;
    }
    
    method void collisionSwitch() {
        let collision = ~collision;
        return;
    }
    
    method void flipDeltaX() {
        let delta_x = -delta_x;
        return;
    }
    
    method void flipDeltaY() {
        let delta_y = -delta_y;
        return;
    }
    
    /** Moves the ball on the screen. */
    method void moveBall() {
        let x = x + delta_x;
        let y = y + delta_y;
        return;
    }
}